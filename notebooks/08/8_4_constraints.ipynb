{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "c5507de6",
   "metadata": {},
   "source": [
    "# 8.4 Constraints"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "e9568869-9464-4695-a972-4ed81a7e6c17",
   "metadata": {},
   "source": [
    "The simplest kind of constraint declaration begins with the keywords `subject to`, a\n",
    "name, and a colon. Even the `subject to` is optional; AMPL assumes that any declaration\n",
    "not beginning with a keyword is a constraint. Following the colon is an algebraic\n",
    "description of the constraint, in terms of previously defined sets, parameters, and\n",
    "variables.\n",
    "\n",
    "Thus, in the production model introduced in\n",
    "[Figure 1-4a](../01/tut_1_4.ipynb#fig-1-4a),\n",
    "we have the following constraint imposed by limited processing time:\n",
    "\n",
    "```ampl\n",
    "subject to Time:\n",
    "  sum {p in PROD} (1 / rate[p]) * Make[p] <= avail;\n",
    "```\n",
    "\n",
    "The name of a constraint, like the name of an objective, is not used elsewhere in an\n",
    "algebraic model, though it figures in alternative *columnwise* formulations (Chapter 16)\n",
    "and is used in the AMPL command environment to specify the constraint's dual value\n",
    "and other associated quantities (Chapter 14) xTODO.\n",
    "\n",
    "Most of the constraints in large linear programming models are defined as indexed\n",
    "collections, by giving an indexing expression after the constraint name. The constraint\n",
    "`Time`, for example, is generalized in subsequent examples to say that the production\n",
    "time may not exceed the time available in each processing stage `s`\n",
    "([Figure 1-6](../01/tut_1_6.ipynb#fig-1-6)):\n",
    "\n",
    "```ampl\n",
    "subject to Time {s in STAGE}:\n",
    "  sum {p in PROD} (1 / rate[p,s]) * Make[p] <= avail[s];\n",
    "```\n",
    "\n",
    "or in each week `t`\n",
    "([Figure 4-4](../04/4_3_a_model_of_production_and_transportation.ipynb#fig-4-4)):\n",
    "\n",
    "```ampl\n",
    "subject to Time {t in 1..T}:\n",
    "  sum {p in PROD} (1 / rate[p]) * Make[p,t] <= avail[t];\n",
    "```\n",
    "\n",
    "Another constraint from the latter example says that production, sales, and inventories\n",
    "must balance for each product `p` in each week `t`:\n",
    "\n",
    "```ampl\n",
    "subject to Balance {p in PROD, t in 1..T}:\n",
    "  Make[p,t] + Inv[p,t-1] = Sell[p,t] + Inv[p,t];\n",
    "```\n",
    "\n",
    "A constraint declaration can specify any valid indexing expression, which defines a set\n",
    "(as explained in Chapters [5](../05/05.md) and [6](../06/06.md)); there is one constraint for each member of this set.\n",
    "The constraint name can be subscripted, so that `Time[1]` or `Balance[p,t+1]` refers\n",
    "to a particular constraint from an indexed collection.\n",
    "\n",
    "The indexing expression in a constraint declaration should specify a dummy index\n",
    "(such as `s`, `t`, or `p` in the preceding examples) for each dimension of the indexing\n",
    "set. When the constraint corresponding to a particular indexing-set member is processed\n",
    "by AMPL, the dummy indices take their values from that member.\n",
    "\n",
    "This use of dummy indices is what permits a single constraint expression to represent\n",
    "many constraints; the indexing expression is AMPL's translation of a phrase such as\n",
    "\"for all products `p` and weeks `t = 1` to `T`\" that might be seen in an algebraic\n",
    "statement of the model.\n",
    "\n",
    "By using more complex indexing expressions, you can specify more precisely the\n",
    "constraints to be included in a model. Consider, for example, the following variation\n",
    "on the production-time constraint:\n",
    "\n",
    "```ampl\n",
    "subject to Time {t in 1..T: avail[t] > 0}:\n",
    "  sum {p in PROD} (1 / rate[p]) * Make[p,t] <= avail[t];\n",
    "```\n",
    "\n",
    "This says that if `avail[t]` is specified as zero in the data for any week `t`, it is to be\n",
    "interpreted as meaning *no constraint on time available in week `t`*, rather than a\n",
    "limit of zero.\n",
    "\n",
    "In the simpler case where there is just one `Time` constraint not indexed over weeks,\n",
    "you can specify an analogous conditional definition as follows:\n",
    "\n",
    "```ampl\n",
    "subject to Time {if avail > 0}:\n",
    "  sum {p in PROD} (1 / rate[p]) * Make[p] <= avail;\n",
    "```\n",
    "\n",
    "The pseudo-indexing expression `{if avail > 0}` causes one constraint, named `Time`,\n",
    "to be generated if the condition `avail > 0` is true, and no constraint at all to be\n",
    "generated if the condition is false. (The same notation can be used to conditionally\n",
    "define other model components.)\n",
    "\n",
    "AMPL's algebraic description of a constraint may consist of any two linear expressions\n",
    "separated by an equality or inequality operator:\n",
    "\n",
    "```\n",
    "linear-expr <= linear-expr\n",
    "linear-expr =  linear-expr\n",
    "linear-expr >= linear-expr\n",
    "```\n",
    "\n",
    "While it is customary in mathematical descriptions of linear programming to place all\n",
    "terms containing variables on the left-hand side of the operator and all other terms on\n",
    "the right (as in constraint `Time`), AMPL imposes no such requirement (as seen in\n",
    "constraint `Balance`).\n",
    "\n",
    "Convenience and readability should determine what terms you place on each side of the\n",
    "operator. AMPL takes care of canonicalizing constraints, such as by combining linear\n",
    "terms involving the same variable and moving variables from one side of a constraint to\n",
    "the other. The `expand` command described in\n",
    "[Section 1.4](../01/tut_1_4.ipynb)\n",
    "shows the canonical forms of the constraints.\n",
    "\n",
    "AMPL also allows double-inequality constraints such as the following from the diet\n",
    "model of\n",
    "[Figure 2-1](../02/2_2_an_AMPL_model_for_the_diet_problem.ipynb#fig-2-1):\n",
    "\n",
    "```ampl\n",
    "subject to Diet {i in NUTR}:\n",
    "  n_min[i] <= sum {j in FOOD} amt[i,j] * Buy[j] <= n_max[i];\n",
    "```\n",
    "\n",
    "This says that the middle expression—the amount of nutrient `i` supplied by all foods—\n",
    "must be greater than or equal to `n_min[i]` and less than or equal to `n_max[i]`.\n",
    "\n",
    "The permissible forms for a constraint of this kind are\n",
    "\n",
    "```\n",
    "const-expr <= linear-expr <= const-expr\n",
    "const-expr >= linear-expr >= const-expr\n",
    "```\n",
    "\n",
    "where each `const-expr` must contain no variables. The effect is to give upper and lower\n",
    "bounds on the value of the `linear-expr`. If your model requires variables in the\n",
    "left-hand or right-hand constant expression, you must define two different constraints\n",
    "in separate declarations.\n",
    "\n",
    "For most applications of linear programming, you need not worry about the form of the\n",
    "constraints. If you simply write the constraints in the most convenient way, they will\n",
    "be recognized as proper linear constraints according to the rules in this chapter. (xTODO talk about MP?)\n",
    "\n",
    "There do exist situations, however, in which your choice of formulation determines\n",
    "whether AMPL recognizes your model as linear. Suppose we want to constrain the\n",
    "production model so that no product `p` may represent more than a certain fraction of\n",
    "total production. Let `max_frac` denote this fraction. A direct formulation would be\n",
    "\n",
    "```ampl\n",
    "subject to Limit {p in PROD}:\n",
    "  Make[p] / sum {q in PROD} Make[q] <= max_frac;\n",
    "```\n",
    "\n",
    "This is not linear to AMPL, because its left-hand expression contains a division by\n",
    "a sum of variables. But if we rewrite it as\n",
    "\n",
    "```ampl\n",
    "subject to Limit {p in PROD}:\n",
    "  Make[p] <= max_frac * sum {q in PROD} Make[q];\n",
    "```\n",
    "\n",
    "then AMPL does recognize it as linear.\n",
    "\n",
    "AMPL simplifies constraints as it prepares the model and data for handing to a solver.\n",
    "For example, it may eliminate variables fixed at a value, combine single-variable\n",
    "constraints with simple bounds on variables, or drop constraints implied by other\n",
    "constraints.\n",
    "\n",
    "You can normally ignore this *presolve* phase, but there are ways to observe its effects\n",
    "and modify its actions, as explained in\n",
    "Section 14.1 xTODO."
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Python 3 (ipykernel)",
   "language": "python",
   "name": "python3"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.10.12"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
