{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "c5507de6",
   "metadata": {},
   "source": [
    "# 8.2 Linear expressions"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "id": "07b80233",
   "metadata": {
    "scrolled": true
   },
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "\u001b[33mWARNING: Error parsing dependencies of gpg: Invalid version: '1.23.2-unknown'\u001b[0m\u001b[33m\n",
      "\u001b[0m\n",
      "\u001b[1m[\u001b[0m\u001b[34;49mnotice\u001b[0m\u001b[1;39;49m]\u001b[0m\u001b[39;49m A new release of pip is available: \u001b[0m\u001b[31;49m25.2\u001b[0m\u001b[39;49m -> \u001b[0m\u001b[32;49m25.3\u001b[0m\n",
      "\u001b[1m[\u001b[0m\u001b[34;49mnotice\u001b[0m\u001b[1;39;49m]\u001b[0m\u001b[39;49m To update, run: \u001b[0m\u001b[32;49mpython3 -m pip install --upgrade pip\u001b[0m\n",
      "Note: you may need to restart the kernel to use updated packages.\n"
     ]
    },
    {
     "data": {
      "application/vnd.jupyter.widget-view+json": {
       "model_id": "ba96942d96684359a12bd8879bbac80e",
       "version_major": 2,
       "version_minor": 0
      },
      "text/plain": [
       "VBox(children=(Output(), HBox(children=(Text(value='', description='License UUID:', style=TextStyle(descriptio…"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "# install dependencies\n",
    "%pip install -q amplpy\n",
    "\n",
    "from amplpy import AMPL, ampl_notebook\n",
    "\n",
    "ampl = ampl_notebook(\n",
    "    modules=['highs'],  # modules to install\n",
    "    license_uuid='default',  # license to use\n",
    ")  # instantiate AMPL object and register magics"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "e9568869-9464-4695-a972-4ed81a7e6c17",
   "metadata": {},
   "source": [
    "An arithmetic expression is *linear* in a given variable if, for every unit increase or\n",
    "decrease in the variable, the value of the expression increases or decreases by some fixed\n",
    "amount. An expression that is linear in all its variables is called a *linear expression*.\n",
    "(Strictly speaking, these are *affine* expressions, and a linear expression is an affine\n",
    "expression with constant term zero. For simplicity, we will ignore this distinction.)\n",
    "\n",
    "AMPL recognizes as a linear expression any sum of terms of the form\n",
    "\n",
    "```text\n",
    "constant-expr\n",
    "variable-ref\n",
    "(constant-expr) * variable-ref\n",
    "```\n",
    "\n",
    "provided that each `constant-expr` is an arithmetic expression that contains no variables,\n",
    "while `variable-ref` is a reference (possibly subscripted) to a variable. The parentheses\n",
    "around the `constant-expr` may be omitted if the result is the same according to the rules\n",
    "of operator precedence (see [Table 7-3](../appendix/appendix.ipynb#table-7-3)).\n",
    "\n",
    "The following examples, taken from the constraints in the multiperiod production model\n",
    "of [Figure 6-3](../06/6_5_indexed_collections_of_sets.ipynb#fig-6-3), are all linear\n",
    "expressions under this definition:\n",
    "\n",
    "```ampl\n",
    "avail[t]\n",
    "Make[p,t] + Inv[p,t-1]\n",
    "sum {p in PROD} (1 / rate[p]) * Make[p,t]\n",
    "sum {a in AREA[p]} Sell[p,a,t] + Inv[p,t]\n",
    "```\n",
    "\n",
    "The model's objective,\n",
    "\n",
    "```ampl\n",
    "sum {p in PROD, t in 1..T}\n",
    "  (sum {a in AREA[p]} revenue[p,a,t] * Sell[p,a,t]\n",
    "   - prodcost[p] * Make[p,t] - invcost[p] * Inv[p,t])\n",
    "```\n",
    "\n",
    "is also linear, because subtraction of a term is the addition of its negative, and a sum of\n",
    "sums is itself a sum.\n",
    "\n",
    "Various kinds of expressions are equivalent to a sum of terms of the forms above, and are\n",
    "also recognized as linear by AMPL. Division by an arithmetic expression is equivalent to\n",
    "multiplication by its inverse, so\n",
    "\n",
    "```ampl\n",
    "(1 / rate[p]) * Make[p,t]\n",
    "```\n",
    "\n",
    "may be written in a linear program as\n",
    "\n",
    "```ampl\n",
    "Make[p,t] / rate[p]\n",
    "```\n",
    "\n",
    "The order of multiplications is irrelevant, so the `variable-ref` need not come at the end\n",
    "of a term; for instance,\n",
    "\n",
    "```ampl\n",
    "revenue[p,a,t] * Sell[p,a,t]\n",
    "```\n",
    "\n",
    "is equivalent to\n",
    "\n",
    "```ampl\n",
    "Sell[p,a,t] * revenue[p,a,t]\n",
    "```\n",
    "\n",
    "As an example combining these principles, suppose that `revenue[p,a,t]` is measured in\n",
    "dollars per metric ton, while `Sell[p,a,t]` remains in tons. If we define conversion factors\n",
    "\n",
    "```ampl\n",
    "param mt_t = 0.90718474;   # metric tons per ton\n",
    "param t_mt = 1 / mt_t;    # tons per metric ton\n",
    "```\n",
    "\n",
    "then both\n",
    "\n",
    "```ampl\n",
    "sum {a in AREA[p]} mt_t * revenue[p,a,t] * Sell[p,a,t]\n",
    "```\n",
    "\n",
    "and\n",
    "\n",
    "```ampl\n",
    "sum {a in AREA[p]} revenue[p,a,t] * Sell[p,a,t] / t_mt\n",
    "```\n",
    "\n",
    "are linear expressions for total revenue.\n",
    "\n",
    "To continue this example, if costs are also in dollars per metric ton, the objective could\n",
    "be written as\n",
    "\n",
    "```ampl\n",
    "mt_t * sum {p in PROD, t in 1..T}\n",
    "   (sum {a in AREA[p]} revenue[p,a,t] * Sell[p,a,t]\n",
    "    - prodcost[p] * Make[p,t] - invcost[p] * Inv[p,t])\n",
    "```\n",
    "\n",
    "or as\n",
    "\n",
    "```ampl\n",
    "sum {p in PROD, t in 1..T}\n",
    "   (sum {a in AREA[p]} revenue[p,a,t] * Sell[p,a,t]\n",
    "    - prodcost[p] * Make[p,t] - invcost[p] * Inv[p,t]) / t_mt\n",
    "```\n",
    "\n",
    "Multiplication and division distribute over any summation to yield an equivalent linear\n",
    "sum of terms. Notice that in the first form, `mt_t` multiplies the entire\n",
    "`sum {p in PROD, t in 1..T}`, while in the second `t_mt` divides only the summand that\n",
    "follows `sum {p in PROD, t in 1..T}`, because the `/` operator has higher precedence than\n",
    "the `sum` operator. In these examples the effect is the same, however.\n",
    "\n",
    "Finally, an `if–then–else` operator produces a linear result if the expressions following\n",
    "`then` and `else` are both linear and no variables appear in the logical expression between\n",
    "`if` and `else`. The following example appeared in a constraint in\n",
    "[Section 7.3](../07/7_3_logical_and_conditional_expressions.ipynb):\n",
    "\n",
    "```ampl\n",
    "Make[j,t] +\n",
    "   (if t = first(WEEKS) then inv0[j] else Inv[j,prev(t)])\n",
    "```\n",
    "\n",
    "The variables in a linear expression may not appear as operands to any other operators, or\n",
    "as arguments to any functions. This rule applies to iterated operators such as `max`,\n",
    "`min`, `abs`, `forall`, and `exists`, as well as to `^` and standard numerical functions\n",
    "such as `sqrt`, `log`, and `cos`.\n",
    "\n",
    "To summarize, a linear expression may be any sum of terms in the forms\n",
    "\n",
    "```text\n",
    "constant-expr\n",
    "variable-ref\n",
    "(constant-expr) * (linear-expr)\n",
    "(linear-expr) * (constant-expr)\n",
    "(linear-expr) / (constant-expr)\n",
    "if logical-expr then linear-expr else linear-expr\n",
    "```\n",
    "\n",
    "where `constant-expr` is any arithmetic expression that contains no references to\n",
    "variables, and `linear-expr` is any other (simpler) linear expression. Parentheses may be\n",
    "omitted if the result is the same according to the rules of operator precedence in\n",
    "[Table 7-3](../appendix/appendix.ipynb#table-7-3). AMPL automatically performs the\n",
    "transformations that convert any such expression to a simple sum of linear terms."
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Python 3 (ipykernel)",
   "language": "python",
   "name": "python3"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.10.12"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
