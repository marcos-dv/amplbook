{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "c5507de6",
   "metadata": {},
   "source": [
    "# 8.1 Variables"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "id": "07b80233",
   "metadata": {
    "scrolled": true
   },
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "\u001b[33mWARNING: Error parsing dependencies of gpg: Invalid version: '1.23.2-unknown'\u001b[0m\u001b[33m\n",
      "\u001b[0m\n",
      "\u001b[1m[\u001b[0m\u001b[34;49mnotice\u001b[0m\u001b[1;39;49m]\u001b[0m\u001b[39;49m A new release of pip is available: \u001b[0m\u001b[31;49m25.2\u001b[0m\u001b[39;49m -> \u001b[0m\u001b[32;49m25.3\u001b[0m\n",
      "\u001b[1m[\u001b[0m\u001b[34;49mnotice\u001b[0m\u001b[1;39;49m]\u001b[0m\u001b[39;49m To update, run: \u001b[0m\u001b[32;49mpython3 -m pip install --upgrade pip\u001b[0m\n",
      "Note: you may need to restart the kernel to use updated packages.\n"
     ]
    },
    {
     "data": {
      "application/vnd.jupyter.widget-view+json": {
       "model_id": "ba96942d96684359a12bd8879bbac80e",
       "version_major": 2,
       "version_minor": 0
      },
      "text/plain": [
       "VBox(children=(Output(), HBox(children=(Text(value='', description='License UUID:', style=TextStyle(descriptio…"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "# install dependencies\n",
    "%pip install -q amplpy\n",
    "\n",
    "from amplpy import AMPL, ampl_notebook\n",
    "\n",
    "ampl = ampl_notebook(\n",
    "    modules=['highs'],  # modules to install\n",
    "    license_uuid='default',  # license to use\n",
    ")  # instantiate AMPL object and register magics"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "e9568869-9464-4695-a972-4ed81a7e6c17",
   "metadata": {},
   "source": [
    "The variables of a linear program have much in common with its numerical parameters.\n",
    "Both are symbols that stand for numbers, and that may be used in arithmetic expressions.\n",
    "Parameter values are supplied by the modeler or computed from other values, while the\n",
    "values of variables are determined by an optimizing algorithm (as implemented in one of\n",
    "the packages that we refer to as *solvers*).\n",
    "\n",
    "Syntactically, variable declarations are the same as the parameter declarations defined\n",
    "in [Chapter 7](../07/07.md), except that they begin with the keyword `var` rather than\n",
    "`param`. The meaning of qualifying phrases within the declaration may be different,\n",
    "however, when these phrases are applied to variables rather than to parameters.\n",
    "\n",
    "Phrases beginning with `>=` or `<=` are by far the most common in declarations of variables\n",
    "for linear programs. They have appeared in all of our examples, beginning with the\n",
    "production model of [Figure 1-4](../01/tut_1_4.ipynb#fig-1-4a):\n",
    "\n",
    "```ampl\n",
    "var Make {p in PROD} >= 0, <= market[p];\n",
    "```\n",
    "\n",
    "This declaration creates an indexed collection of variables `Make[p]`, one for each member\n",
    "`p` of the set `PROD`; the rules in this respect are exactly the same as for parameters.\n",
    "The effect of the two qualifying phrases is to impose a restriction, or *constraint*, on the\n",
    "permissible values of the variables. Specifically, `>= 0` implies that all of the variables\n",
    "`Make[p]` must be assigned nonnegative values by the optimizing algorithm, while the\n",
    "phrase `<= market[p]` says that, for each product `p`, the value given to `Make[p]` may\n",
    "not exceed the value of the parameter `market[p]`.\n",
    "\n",
    "In general, either `>=` or `<=` may be followed by any arithmetic expression involving\n",
    "previously defined sets and parameters and currently defined dummy indices. Most linear\n",
    "programs are formulated in such a way that every variable must be nonnegative; an AMPL\n",
    "variable declaration can specify nonnegativity either directly by `>= 0`, or indirectly as\n",
    "in the diet model of [Figure 5-1](../05/5_3_set_operations.ipynb#fig-5-1):\n",
    "\n",
    "```ampl\n",
    "param f_min {FOOD} >= 0;\n",
    "param f_max {j in FOOD} >= f_min[j];\n",
    "\n",
    "var Buy {j in FOOD} >= f_min[j], <= f_max[j];\n",
    "```\n",
    "\n",
    "The values following `>=` and `<=` are lower and upper bounds on the variables. Because\n",
    "these bounds represent a kind of constraint, they could just as well be imposed by the\n",
    "constraint declarations described later in this chapter. By placing bounds in the `var`\n",
    "declaration instead, you may be able to make the model shorter or clearer, although you\n",
    "will not make the optimal solution any different or easier to find. Some solvers do treat\n",
    "bounds specially in order to speed up their algorithms, but with AMPL all bounds are\n",
    "identified automatically, no matter how they are expressed in your model.\n",
    "\n",
    "Variable declarations may not use the comparison operators `<`, `>` or `<>` in qualifying\n",
    "phrases. For linear programming it makes no sense to constrain a variable to be, say,\n",
    "`< 3`, since it could always be chosen as `2.99999…` or as close to `3` as you like.\n",
    "\n",
    "An `=` phrase in a variable declaration gives rise to a definition, as in a parameter\n",
    "declaration. Because a variable is being declared, however, the expression to the right of\n",
    "the `=` operator may contain previously declared variables as well as sets and parameters.\n",
    "For example, instead of writing the complicated objective from the multi-period\n",
    "production model of [Figure 6-3](../06/6_5_indexed_collections_of_sets.ipynb#fig-6-3)\n",
    "(`steelT3.mod`) as\n",
    "\n",
    "```ampl\n",
    "maximize Total_Profit:\n",
    "  sum {p in PROD, t in 1..T}\n",
    "     (sum {a in AREA[p]} revenue[p,a,t]*Sell[p,a,t]\n",
    "      - prodcost[p]*Make[p,t] - invcost[p]*Inv[p,t]);\n",
    "```\n",
    "\n",
    "you could instead define variables to represent the total revenues, production costs, and\n",
    "inventory costs:\n",
    "\n",
    "```ampl\n",
    "var Total_Revenue =\n",
    "  sum {p in PROD, t in 1..T}\n",
    "     sum {a in AREA[p]} revenue[p,a,t] * Sell[p,a,t];\n",
    "\n",
    "var Total_Prod_Cost =\n",
    "  sum {p in PROD, t in 1..T} prodcost[p] * Make[p,t];\n",
    "\n",
    "var Total_Inv_Cost =\n",
    "  sum {p in PROD, t in 1..T} invcost[p] * Inv[p,t];\n",
    "```\n",
    "\n",
    "The objective would then be the sum of these three defined variables:\n",
    "\n",
    "```ampl\n",
    "maximize Total_Profit:\n",
    "  Total_Revenue - Total_Prod_Cost - Total_Inv_Cost;\n",
    "```\n",
    "\n",
    "The structure of the objective is clearer this way. Also, the defined variables are\n",
    "conveniently available to a `display` statement to show how the three main components of\n",
    "profit compare:\n",
    "\n",
    "```\n",
    "ampl.display(\"Total_Revenue, Total_Prod_Cost, Total_Inv_Cost\")\n",
    "\n",
    "# Output:\n",
    "# Total_Revenue = 801385\n",
    "# Total_Prod_Cost = 285643\n",
    "# Total_Inv_Cost = 1221\n",
    "```\n",
    "\n",
    "Declarations of defined variables like these do not give rise to additional constraints in\n",
    "the resulting problem instance. Rather, the linear expression to the right of the `=` is\n",
    "substituted for every occurrence of the defined variable in the objective and constraints.\n",
    "Defined variables are even more useful for nonlinear programming, where the substitution\n",
    "may be only implicit, so we will return to this topic in Chapter 18 xTODO.\n",
    "\n",
    "If the expression to the right of the `=` operator contains no variables, then you are\n",
    "merely defining variables to be fixed to values given by the data. In that case you should\n",
    "use a `param` declaration instead. On the other hand, if you only want to fix some\n",
    "variables temporarily while developing or analyzing a model, then you should leave the\n",
    "declarations unchanged and instead fix them with the `fix` command described in\n",
    "[Section 11.4] xTODO.\n",
    "\n",
    "A `:=` or `default` phrase in a variable declaration gives initial values to the indicated\n",
    "variables. Variables not assigned an initial value by `:=` can also be assigned initial\n",
    "values from a data file. Initial values of variables are normally changed—ideally to\n",
    "optimal values—when a solver is invoked. Thus the main purpose of initial values of\n",
    "variables is to give the solver a good starting solution. Solvers for linear programming can\n",
    "seldom make good use of a starting solution, however, so we defer further discussion of\n",
    "this topic to Chapter 18 on nonlinear programming.\n",
    "\n",
    "Finally, variables may be declared as `integer` so that they must take whole-number\n",
    "values in any optimal solution, or as `binary` so that they may only take the values `0`\n",
    "and `1`. Models that contain any such variables are *integer programs*, which are the\n",
    "topic of Chapter 20 xTODO.\n"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Python 3 (ipykernel)",
   "language": "python",
   "name": "python3"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.10.12"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
