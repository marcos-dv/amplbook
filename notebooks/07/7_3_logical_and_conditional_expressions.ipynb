{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "c5507de6",
   "metadata": {},
   "source": [
    "# 7.3 Logical and conditional expressions"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "id": "07b80233",
   "metadata": {
    "scrolled": true
   },
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "\u001b[33mWARNING: Error parsing dependencies of gpg: Invalid version: '1.23.2-unknown'\u001b[0m\u001b[33m\n",
      "\u001b[0m\n",
      "\u001b[1m[\u001b[0m\u001b[34;49mnotice\u001b[0m\u001b[1;39;49m]\u001b[0m\u001b[39;49m A new release of pip is available: \u001b[0m\u001b[31;49m25.2\u001b[0m\u001b[39;49m -> \u001b[0m\u001b[32;49m25.3\u001b[0m\n",
      "\u001b[1m[\u001b[0m\u001b[34;49mnotice\u001b[0m\u001b[1;39;49m]\u001b[0m\u001b[39;49m To update, run: \u001b[0m\u001b[32;49mpython3 -m pip install --upgrade pip\u001b[0m\n",
      "Note: you may need to restart the kernel to use updated packages.\n"
     ]
    },
    {
     "data": {
      "application/vnd.jupyter.widget-view+json": {
       "model_id": "ba96942d96684359a12bd8879bbac80e",
       "version_major": 2,
       "version_minor": 0
      },
      "text/plain": [
       "VBox(children=(Output(), HBox(children=(Text(value='', description='License UUID:', style=TextStyle(descriptioâ€¦"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "# install dependencies\n",
    "%pip install -q amplpy\n",
    "\n",
    "from amplpy import AMPL, ampl_notebook\n",
    "\n",
    "ampl = ampl_notebook(\n",
    "    modules=['highs'],  # modules to install\n",
    "    license_uuid='default',  # license to use\n",
    ")  # instantiate AMPL object and register magics"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "e9568869-9464-4695-a972-4ed81a7e6c17",
   "metadata": {},
   "source": [
    "The values of arithmetic expressions can be tested against each other by comparison\n",
    "operators:\n",
    "```\n",
    "=        equal to\n",
    "<>       not equal to\n",
    "<        less than\n",
    "<=       less than or equal to\n",
    ">        greater than\n",
    ">=       greater than or equal to\n",
    "```\n",
    "The result of a comparison is either \"true\" or \"false\". Thus `T > 1` is true if the parameter\n",
    "`T` has a value greater than 1, and is false otherwise; and\n",
    "```\n",
    "sum {i in ORIG} supply[i] = sum {j in DEST} demand[j]\n",
    "```\n",
    "is true if and only if total supply equals total demand.\n",
    "\n",
    "Comparisons are one example of AMPL's logical expressions, which evaluate to true\n",
    "or false. Set membership tests using in and within, described in [Section 5.4](../05/5_4_set_membership_operations_and_functions.ipynb),\n",
    "are another example. More complex logical expressions can be built up with logical operators.\n",
    "The and operator returns true if and only if both its operands are true, while or\n",
    "returns true if and only if at least one of its operands is true; the unary operator not\n",
    "returns false for true and true for false. Thus the expression\n",
    "```\n",
    "T >= 0 and T <= 10\n",
    "```\n",
    "is only true if `T` lies in the interval `[0, 10]`, while the following from [Section 5.5](../05/5_5_indexing_expressions.ipynb),\n",
    "```\n",
    "i in MAXREQ or n_min[i] > 0\n",
    "```\n",
    "is true if i is a member of `MAXREQ`, or `n_min[i]` is positive, or both. Where several\n",
    "operators are used together, any comparison, membership or arithmetic operator has\n",
    "higher precedence than the logical operators; and has higher precedence than or, while\n",
    "not has higher precedence than either. Thus the expression\n",
    "```\n",
    "not i in MAXREQ or n_min[i] > 0 and n_min[i] <= 10\n",
    "```\n",
    "\n",
    "is interpreted as\n",
    "```\n",
    "(not (i in MAXREQ)) or ((n_min[i] > 0) and (n_min[i] <= 10))\n",
    "```\n",
    "Alternatively, the not in operator could be used:\n",
    "```\n",
    "i not in MAXREQ or n_min[i] > 0 and n_min[i] <= 10\n",
    "```\n",
    "The precedences are summarized in [Table 7-1](../07/7_2_arithmetic_expressions.ipynb#table-7-1), which also gives alternative forms.\n",
    "\n",
    "Like + and \\*, the operators or and and have iterated versions. The iterated or is\n",
    "denoted by exists, and the iterated and by forall. For example, the expression\n",
    "```\n",
    "exists {i in ORIG} demand[i] > 10\n",
    "```\n",
    "is true if and only if at least one origin has a demand greater than 10, while\n",
    "```\n",
    "forall {i in ORIG} demand[i] > 10\n",
    "```\n",
    "is true if and only if every origin has demand greater than 10.\n",
    "\n",
    "Another use for a logical expression is as an operand to the conditional or if-thenelse\n",
    "operator, which returns one of two different arithmetic values depending on\n",
    "whether the logical expression is true or false. Consider the two collections of inventory\n",
    "balance constraints in the multiperiod production model of [Figure 5-3](../05/5_6_ordered_sets.ipynb#fig-5-3):\n",
    "```\n",
    "subject to Balance0 {p in PROD}:\n",
    "  Make[p,first(WEEKS)] + inv0[p]\n",
    "     = Sell[p,first(WEEKS)] + Inv[p,first(WEEKS)];\n",
    "subject to Balance {p in PROD, t in WEEKS: ord(t) > 1}:\n",
    "  Make[p,t] + Inv[p,prev(t)] = Sell[p,t] + Inv[p,t];\n",
    "```\n",
    "The `Balance0` constraints are basically the `Balance` constraints with `t` set to\n",
    "`first(WEEKS)`. The only difference is in the second term, which represents the previous\n",
    "week's inventory; it is given as `inv0[p]` for the first week (in the `Balance0` constraints)\n",
    "but is represented by the variable `Inv[p,prev(t)]` for subsequent weeks (in\n",
    "the `Balance` constraints). We would like to combine these constraints into one\n",
    "declaration, by having a term that takes the value `inv0[p]` when `t` is the first week, and takes\n",
    "the value `Inv[p,prev(t)]` otherwise. Such a term is written in AMPL as:\n",
    "```\n",
    "if t = first(WEEKS) then inv0[p] else Inv[p,prev(t)]\n",
    "```\n",
    "Placing this expression into the constraint declaration, we can write\n",
    "```\n",
    "subject to Balance {p in PROD, t in WEEKS}:\n",
    "  Make[p,t] +\n",
    "     (if t = first(WEEKS) then inv0[p] else Inv[p,prev(t)])\n",
    "\t= Sell[p,t] + Inv[p,t];\n",
    "```\n",
    "This form communicates the inventory balance constraints more concisely and directly\n",
    "than two separate declarations.\n",
    "\n",
    "The general form of a conditional expression is\n",
    "```\n",
    "if a then b else c\n",
    "```\n",
    "\n",
    "where `a` is a logical expression. If `a` evaluates to true, the conditional expression takes the\n",
    "value of `b`; if `a` is false, the expression takes the value of `c`. If `c` is zero, the `else c` part\n",
    "can be dropped. Most often `b` and `c` are arithmetic expressions, but they can also be string\n",
    "or set expressions, so long as both are expressions of the same kind. Because then and\n",
    "else have lower precedence than any other operators, a conditional expression needs to\n",
    "be parenthesized (as in the example above) unless it occurs at the end of a statement.\n",
    "\n",
    "AMPL also has an if-then-else for use in programming; like the conditional statements\n",
    "in many programming languages, it executes one or another block of statements\n",
    "depending on the truth of some logical expression. We describe it with other AMPL programming\n",
    "features in Chapter 13 xTODO. The if-then-else that we have described here is\n",
    "not a statement, but rather an expression whose value is conditionally determined. It\n",
    "therefore belongs inside a declaration, in a place where an expression would normally be\n",
    "evaluated.\n",
    "\n",
    "xTODO ==> Mention new chapter about logic or MP\n"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Python 3 (ipykernel)",
   "language": "python",
   "name": "python3"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.10.12"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
