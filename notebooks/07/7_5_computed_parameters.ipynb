{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "c5507de6",
   "metadata": {},
   "source": [
    "# 7.5 Computed parameters"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "id": "07b80233",
   "metadata": {
    "scrolled": true
   },
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "\u001b[33mWARNING: Error parsing dependencies of gpg: Invalid version: '1.23.2-unknown'\u001b[0m\u001b[33m\n",
      "\u001b[0m\n",
      "\u001b[1m[\u001b[0m\u001b[34;49mnotice\u001b[0m\u001b[1;39;49m]\u001b[0m\u001b[39;49m A new release of pip is available: \u001b[0m\u001b[31;49m25.2\u001b[0m\u001b[39;49m -> \u001b[0m\u001b[32;49m25.3\u001b[0m\n",
      "\u001b[1m[\u001b[0m\u001b[34;49mnotice\u001b[0m\u001b[1;39;49m]\u001b[0m\u001b[39;49m To update, run: \u001b[0m\u001b[32;49mpython3 -m pip install --upgrade pip\u001b[0m\n",
      "Note: you may need to restart the kernel to use updated packages.\n"
     ]
    },
    {
     "data": {
      "application/vnd.jupyter.widget-view+json": {
       "model_id": "ba96942d96684359a12bd8879bbac80e",
       "version_major": 2,
       "version_minor": 0
      },
      "text/plain": [
       "VBox(children=(Output(), HBox(children=(Text(value='', description='License UUID:', style=TextStyle(descriptioâ€¦"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "# install dependencies\n",
    "%pip install -q amplpy\n",
    "\n",
    "from amplpy import AMPL, ampl_notebook\n",
    "\n",
    "ampl = ampl_notebook(\n",
    "    modules=['highs'],  # modules to install\n",
    "    license_uuid='default',  # license to use\n",
    ")  # instantiate AMPL object and register magics"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "e9568869-9464-4695-a972-4ed81a7e6c17",
   "metadata": {},
   "source": [
    "It is seldom possible to arrange that the data values available to a model are precisely\n",
    "the coefficient values required by the objective and constraints. Even in the simple production\n",
    "model of [Figure 1-4](../01/tut_1_4.ipynb#fig-1-4a), for example, we wrote the constraint as\n",
    "```\n",
    "sum {p in PROD} (1/rate[p]) * Make[p] <= avail;\n",
    "```\n",
    "because production rates were given in tons per hour, while the coefficient of `Make[p]`\n",
    "had to be in hours per ton. Any parameter expression may be used in the constraints and\n",
    "objective, but the expressions are best kept simple. When more complex expressions are\n",
    "needed, the model is usually easier to understand if new, computed parameters are\n",
    "defined in terms of the data parameters.\n",
    "\n",
    "The declaration of a computed parameter has an assignment phrase, which resembles\n",
    "the restriction phrase described in the previous section except for the use of an = operator\n",
    "to indicate that the parameter is being set equal to a certain expression, rather than merely\n",
    "being restricted by an inequality. As a first example, suppose that the data values provided\n",
    "to the multicommodity transportation model of [Figure 4-1](../04/4_1_a_multicommodity_transportation_model.ipynb#fig-4-1)\n",
    "consist of the total demand for each product, together with each destination's share of demand.\n",
    "The destinations' shares are percentages between zero and 100, but their sum over all destinations\n",
    "might not exactly equal 100%, because of rounding and approximation. Thus we declare\n",
    "data parameters to represent the shares, and a computed parameter equal to their sum:\n",
    "```\n",
    "param share {DEST} >= 0, <= 100;\n",
    "param tot_sh = sum {j in DEST} share[j];\n",
    "```\n",
    "We can then declare a data parameter to represent total demands, and a computed parameter\n",
    "that equals demand at each destination:\n",
    "```\n",
    "param tot_dem {PROD} >= 0;\n",
    "param demand {j in DEST, p in PROD}\n",
    "       = share[j] * tot_dem[p] / tot_sh;\n",
    "```\n",
    "The division by tot_sh acts as a correction factor for a sum not equal to 100%. Once\n",
    "demand has been defined in this way, the model can use it as in [Figure 4-1](../04/4_1_a_multicommodity_transportation_model.ipynb#fig-4-1):\n",
    "```\n",
    "subject to Demand {j in DEST, p in PROD}:\n",
    "\tsum {i in ORIG} Trans[i,j,p] = demand[j,p];\n",
    "```\n",
    "We could avoid computed parameters by substituting the formulas for `tot_sh` and\n",
    "`demand[j,p]` directly into this constraint:\n",
    "```\n",
    "subject to Demand {j in DEST, p in PROD}:\n",
    "  sum {i in ORIG} Trans[i,j,p]\n",
    "     = share[j] * tot_dem[p] / sum {k in DEST} share[k];\n",
    "```\n",
    "This alternative makes the model a little shorter, but the computation of the demand and\n",
    "the structure of the constraint are both harder to follow.\n",
    "\n",
    "As another example, consider a scenario for the multiperiod production model ([Figure 4-4](../04/4_3_a_model_of_production_and_transportation.ipynb#fig-4-4))\n",
    "in which minimum inventories are computed. Specifically, suppose that the inventory\n",
    "of product `p` for week `t` must be at least a certain fraction of `market[p,t+1]`, the\n",
    "maximum that can be sold in the following week. We thus use the following declarations\n",
    "for the data to be supplied:\n",
    "```\n",
    "param frac > 0;\n",
    "param market {PROD,1..T+1} >= 0;\n",
    "```\n",
    "and then declare\n",
    "```\n",
    "param mininv {p in PROD, t in 0..T} = frac * market[p,t+1];\n",
    "var Inv {p in PROD, t in 0..T} >= mininv[p,t];\n",
    "```\n",
    "to define and use parameters `mininv[p,t]` that represent the minimum inventory of\n",
    "product `p` for week `t`. AMPL keeps all = definitions of parameters up to date throughout\n",
    "a session. Thus for example if you change the value of frac the values of all the\n",
    "mininv parameters automatically change accordingly.\n",
    "\n",
    "If you define a computed parameter as in the examples above, then you cannot also\n",
    "specify a data value for it. An attempt to do so will result in an error message:\n",
    "```\n",
    "mininv was defined in the model\n",
    "context: param >>> mininv <<< :=   bands 2 3000\n",
    "```\n",
    "However, there is an alternative way in which you can define an initial value for a parameter\n",
    "but allow it to be changed later.\n",
    "\n",
    "If you define a parameter using the `default` operator in place of `=`, then the parameter\n",
    "is initialized rather than defined. Its value is taken from the value of the expression to\n",
    "the right of the default operator, but does not change if the expression's value later\n",
    "changes. Initial values can be overridden by data statements, and they also may be\n",
    "changed by subsequent assignment statements. This feature is most useful for writing\n",
    "AMPL scripts that update certain values repeatedly, as shown in Section 13.2 xTODO.\n",
    "\n",
    "If you define a parameter using the operator `default` in place of `=`, then you can\n",
    "specify values in data statements to override the ones that would otherwise be computed.\n",
    "For instance, by declaring\n",
    "```\n",
    "param mininv {p in PROD, t in 0..T}\n",
    "   default frac * market[p,t+1];\n",
    "```\n",
    "you can allow a few exceptional minimum inventories to be specified as part of the data\n",
    "for the model, either in a list:\n",
    "```\n",
    "mininv = {\n",
    "    ('bands', 2): 3000,\n",
    "    ('coils', 2): 2000,\n",
    "    ('coils', 3): 2000\n",
    "}\n",
    "ampl.param['mininv'] = mininv\n",
    "```\n",
    "or in a table:\n",
    "```\n",
    "market = {\n",
    "    ('bands', 2): 3000,\n",
    "    ('coils', 2): 2000,\n",
    "    ('coils', 3): 2000\n",
    "}\n",
    "ampl.param['market'] = market\n",
    "```\n",
    "\n",
    "The expression that gives the default value of a parameter is evaluated only when the\n",
    "parameter's value is first needed, such as when an objective or constraint that uses the\n",
    "parameter is processed by a solve command.\n",
    "\n",
    "In most `=` and `default` phrases, the operator is followed by an arithmetic expression\n",
    "in previously defined sets and parameters (but not variables) and currently defined\n",
    "dummy indices. Some parameters in an indexed collection may be given a computed or\n",
    "default value in terms of others in the same collection, however. As an example, you can\n",
    "smooth out some of the variation in the minimum inventories by defining the `mininv`\n",
    "parameter to be a running average like this:\n",
    "```\n",
    "param mininv {p in PROD, t in 0..T} =\n",
    "   if t = 0 then inv0[p]\n",
    "      else 0.5 * (mininv[p,t-1] + frac * market[p,t+1]);\n",
    "```\n",
    "The values of `mininv` for week 0 are set explicitly to the initial inventories, while the\n",
    "values for each subsequent week `t` are defined in terms of the previous week's values.\n",
    "AMPL permits any \"recursive\" definition of this kind, but will signal an error if it detects\n",
    "a circular reference that causes a parameter's value to depend directly or indirectly on\n",
    "itself.\n",
    "\n",
    "You can use the phrases defined in this section together with the restriction phrases of\n",
    "the previous section, to further check the values that are computed. For example the declaration\n",
    "```\n",
    "param mininv {p in PROD, t in 0..T}\n",
    "   = frac * market[p,t+1], >= 0;\n",
    "```\n",
    "\n",
    "will cause an error to be signaled if the computed value of any of the mininv parameters\n",
    "is negative. This check is triggered whenever an AMPL session uses mininv for any\n",
    "purpose."
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Python 3 (ipykernel)",
   "language": "python",
   "name": "python3"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.10.12"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
