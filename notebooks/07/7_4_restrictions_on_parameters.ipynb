{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "c5507de6",
   "metadata": {},
   "source": [
    "# 7.4 Restrictions on parameters"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "id": "07b80233",
   "metadata": {
    "scrolled": true
   },
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "\u001b[33mWARNING: Error parsing dependencies of gpg: Invalid version: '1.23.2-unknown'\u001b[0m\u001b[33m\n",
      "\u001b[0m\n",
      "\u001b[1m[\u001b[0m\u001b[34;49mnotice\u001b[0m\u001b[1;39;49m]\u001b[0m\u001b[39;49m A new release of pip is available: \u001b[0m\u001b[31;49m25.2\u001b[0m\u001b[39;49m -> \u001b[0m\u001b[32;49m25.3\u001b[0m\n",
      "\u001b[1m[\u001b[0m\u001b[34;49mnotice\u001b[0m\u001b[1;39;49m]\u001b[0m\u001b[39;49m To update, run: \u001b[0m\u001b[32;49mpython3 -m pip install --upgrade pip\u001b[0m\n",
      "Note: you may need to restart the kernel to use updated packages.\n"
     ]
    },
    {
     "data": {
      "application/vnd.jupyter.widget-view+json": {
       "model_id": "ba96942d96684359a12bd8879bbac80e",
       "version_major": 2,
       "version_minor": 0
      },
      "text/plain": [
       "VBox(children=(Output(), HBox(children=(Text(value='', description='License UUID:', style=TextStyle(descriptio…"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "# install dependencies\n",
    "%pip install -q amplpy\n",
    "\n",
    "from amplpy import AMPL, ampl_notebook\n",
    "\n",
    "ampl = ampl_notebook(\n",
    "    modules=['highs'],  # modules to install\n",
    "    license_uuid='default',  # license to use\n",
    ")  # instantiate AMPL object and register magics"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "e9568869-9464-4695-a972-4ed81a7e6c17",
   "metadata": {},
   "source": [
    "If T is intended to represent the number of weeks in a multiperiod model, it should be\n",
    "an integer and greater than 1. By including these conditions in T's declaration,\n",
    "```\n",
    "param T > 1 integer;\n",
    "```\n",
    "you instruct AMPL to reject your data if you inadvertently set `T` to 1:\n",
    "```\n",
    "error processing param T:\n",
    "\tfailed check: param T = 1\n",
    "       is not > 1;\n",
    "```\n",
    "or to 2.5:\n",
    "```\n",
    "error processing param T:\n",
    "\tfailed check: param T = 2.5\n",
    "\t\tis not an integer;\n",
    "```\n",
    "AMPL will not send your problem instance to a solver as long as any errors of this kind\n",
    "remain.\n",
    "\n",
    "In the declaration of an indexed collection of parameters, a simple restriction such as\n",
    "integer or >= 0 applies to every parameter defined. Our examples often use this\n",
    "option to specify that vectors and arrays are nonnegative:\n",
    "```\n",
    "param demand {DEST,PROD} >= 0;\n",
    "```\n",
    "If you include dummy indices in the indexing expression, however, you can use them to\n",
    "specify a different restriction for each parameter:\n",
    "```\n",
    "param f_min {FOOD} >= 0;\n",
    "param f_max {j in FOOD} >= f_min[j];\n",
    "```\n",
    "\n",
    "The effect of these declarations is to define a pair of parameters\n",
    "`f_max[j] >= f_min[j]` for every `j` in the set `FOOD`.\n",
    "\n",
    "A restriction phrase for a parameter declaration may be the word integer or\n",
    "binary or a comparison operator followed by an arithmetic expression. While\n",
    "integer restricts a parameter to integral (whole-number) values, binary restricts it to\n",
    "zero or one. The arithmetic expression may refer to sets and parameters previously\n",
    "defined in the model, and to dummy indices defined by the current declaration. There\n",
    "may be several restriction phrases in the same declaration, in which case they may\n",
    "optionally be separated by commas.\n",
    "\n",
    "In special circumstances, a restriction phrase may even refer to the parameter in\n",
    "whose declaration it appears. Some multiperiod production models, for example, are\n",
    "defined in terms of a parameter `cumulative_market[p,t]` that represents the\n",
    "cumulative demand for product `p` in weeks 1 through `t`. Since cumulative demand does\n",
    "not decrease, you might try to write a restriction phrase like this:\n",
    "```\n",
    "param cumulative_market {p in PROD, t in 1..T}\n",
    "  >= cumulative_market[p,t-1];   # ERROR\n",
    "```\n",
    "For the parameters `cumulative_market[p,1]`, however, the restriction phrase will\n",
    "refer to `cumulative_market[p,0]`, which is undefined; AMPL will reject the declaration\n",
    "with an error message. What you need here again is a conditional expression that\n",
    "handles the first period specially:\n",
    "```\n",
    "param cumulative_market {p in PROD, t in 1..T}\n",
    "  >= if t = 1 then 0 else cumulative_market[p,t-1];\n",
    "```\n",
    "The same thing could be written a little more compactly as\n",
    "```\n",
    "param cumulative_market {p in PROD, t in 1..T}\n",
    "  >= if t > 1 then cumulative_market[p,t-1];\n",
    "```\n",
    "since \"else 0\" is assumed. Almost always, some form of if-then-else expression\n",
    "is needed to make this kind of self-reference possible.\n",
    "\n",
    "As you might suspect from this last example, sometimes it is desirable to place a more\n",
    "complex restriction on the model's data than can be expressed by a restriction phrase\n",
    "within a declaration. This is the purpose of the `check` statement. For example, in the\n",
    "transportation model of [Figure 3-1a](./3_2_an_AMPL_model_for_the_transportation_problem.ipynb#fig-3-1a), total supply must equal total demand:\n",
    "```\n",
    "check: sum {i in ORIG} supply[i] = sum {j in DEST} demand[j];\n",
    "```\n",
    "The multicommodity version, in [Figure 4-1](../04/4_1_a_multicommodity_transportation_model.ipynb#fig-4-1), uses an indexed check to say that total supply must equal total demand for each product:\n",
    "```\n",
    "check {p in PROD}:\n",
    "  sum {i in ORIG} supply[i,p] = sum {j in DEST} demand[j,p];\n",
    "```\n",
    "Here the restriction is tested once for each member `p` of `PROD`. If the check fails for any\n",
    "member, AMPL prints an error message and rejects all of the data.\n",
    "\n",
    "You can think of the `check` statement as specifying a kind of constraint, but only on\n",
    "the data. The restriction clause is a logical expression, which may use any previously\n",
    "defined sets and parameters as well as dummy indices defined in the statement's indexing\n",
    "expression. After the data values have been read, the logical expression must evaluate to\n",
    "true; if an indexing expression has been specified, the logical expression is evaluated separately\n",
    "for each assignment of set members to the dummy indices, and must be true for\n",
    "each.\n",
    "\n",
    "We strongly recommend the use of restriction phrases and `check` statements to validate\n",
    "a model's data. These features will help you to catch data errors at an early stage,\n",
    "when they are easy to fix. Data errors not caught will, at best, cause errors in the generation\n",
    "of the variables and constraints, so that you will get some kind of error message from\n",
    "AMPL. In other cases, data errors lead to the generation of an incorrect linear program. If\n",
    "you are fortunate, the incorrect LP will have a meaningless optimal solution, so that —\n",
    "possibly after a good deal of effort — you will be able to work backward to find the error\n",
    "in the data. At worst, the incorrect LP will have a plausible solution, and the error will go\n",
    "undetected.\n"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Python 3 (ipykernel)",
   "language": "python",
   "name": "python3"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.10.12"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
