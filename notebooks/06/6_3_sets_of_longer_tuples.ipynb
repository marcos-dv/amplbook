{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "c5507de6",
   "metadata": {},
   "source": [
    "# 6.3 Sets of longer tuples"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "e9568869-9464-4695-a972-4ed81a7e6c17",
   "metadata": {},
   "source": [
    "AMPL's notation for ordered pairs extends in a natural way to triples, quadruples, or\n",
    "ordered lists of any length. All tuples in a set must have the same dimension. A set can't\n",
    "contain both pairs and triples, for example, nor can the determination as to whether a set\n",
    "contains pairs or triples be made according to some value in the data.\n",
    "\n",
    "The multicommodity transportation model of [Figure 4-1](../04/4_1_a_multicommodity_transportation_model.ipynb#fig-4-1) offers some examples of how\n",
    "we can use ordered triples, and by extension longer tuples. In the original version of the\n",
    "model, the costs and amounts shipped are indexed over origin-destination-product triples:\n",
    "```\n",
    "param cost {ORIG,DEST,PROD} >= 0;\n",
    "var Trans {ORIG,DEST,PROD} >= 0;\n",
    "```\n",
    "In the objective, cost and Trans are written with three subscripts, and the total cost is\n",
    "determined by summing over all triples:\n",
    "```\n",
    "minimize Total_Cost:\n",
    "   sum {i in ORIG, j in DEST, p in PROD}\n",
    "      cost[i,j,p] * Trans[i,j,p];\n",
    "```\n",
    "The indexing expressions are the same as before, except that they list three sets instead of\n",
    "two. An indexing expression that listed `k` sets would similarly denote a set of k-tuples.\n",
    "\n",
    "If instead we define LINKS as we did in [Figure 6-2a](../06/6_2_subsets_and_slices_of_ordered_pairs.ipynb#fig-6-2a), the multicommodity declarations\n",
    "come out like this:\n",
    "```\n",
    "set LINKS within {ORIG,DEST};\n",
    "param cost {LINKS,PROD} >= 0;\n",
    "var Trans {LINKS,PROD} >= 0;\n",
    "minimize Total_Cost:\n",
    "   sum {(i,j) in LINKS, p in PROD} cost[i,j,p] * Trans[i,j,p];\n",
    "```\n",
    "Here we see how a set of triples can be specified as combinations from a set of pairs\n",
    "(`LINKS`) and a set of single members (`PROD`). Since `cost` and `Trans` are indexed over\n",
    "`{LINKS,PROD}`, their first two subscripts must come from a pair in `LINKS`, and their\n",
    "third subscript from a member of `PROD`. Sets of longer tuples can be built up in an analogous\n",
    "way.\n",
    "\n",
    "As a final possibility, it may be that only certain combinations of origins, destinations,\n",
    "and products are workable. Then it makes sense to define a set that contains only the triples\n",
    "of allowed combinations:\n",
    "```\n",
    "set ROUTES within {ORIG,DEST,PROD};\n",
    "```\n",
    "The costs and amounts shipped are indexed over this set:\n",
    "```\n",
    "param cost {ROUTES} >= 0;\n",
    "var Trans {ROUTES} >= 0;\n",
    "```\n",
    "and in the objective, the total cost is a sum over all triples in this set:\n",
    "```\n",
    "minimize Total_Cost:\n",
    "  sum {(i,j,p) in ROUTES} cost[i,j,p] * Trans[i,j,p];\n",
    "```\n",
    "Individual triples are written, by analogy with pairs, as a parenthesized and comma-separated\n",
    "list `(i,j,p)`. Longer lists specify longer tuples.\n",
    "\n",
    "In the three constraints of this model, the summations must be taken over three different\n",
    "slices through the set `ROUTES`:\n",
    "```\n",
    "subject to Supply {i in ORIG, p in PROD}:\n",
    "  sum {(i,j,p) in ROUTES} Trans[i,j,p] = supply[i,p];\n",
    "subject to Demand {j in DEST, p in PROD}:\n",
    "  sum {(i,j,p) in ROUTES} Trans[i,j,p] = demand[j,p];\n",
    "subject to Multi {i in ORIG, j in DEST}:\n",
    "  sum {(i,j,p) in ROUTES} Trans[i,j,p] <= limit[i,j];\n",
    "```\n",
    "In the `Supply` constraint, for instance, indices `i` and `p` are defined before the sum, so\n",
    "`{(i,j,p) in ROUTES}` refers to all `j` such that `(i,j,p)` is a triple in `ROUTES`.\n",
    "AMPL allows comparable slices through any set of tuples, in any number of dimensions\n",
    "and any combination of coordinates.\n",
    "\n",
    "When you declare a high-dimensional set such as `ROUTES`, a phrase like within\n",
    "`{ORIG,DEST,PROD}` may specify a set with a huge number of members. With 10 origins,\n",
    "100 destinations and 100 products, for instance, this set potentially has 100,000\n",
    "members. Fortunately, AMPL does not create this set when it processes the declaration,\n",
    "but merely checks that each tuple in the data for `ROUTES` has its first component in\n",
    "`ORIG`, its second in `DEST`, and its third in `PROD`. The set `ROUTES` can thus be handled\n",
    "efficiently so long as it does not itself contain a huge number of triples.\n",
    "\n",
    "When using high-dimensional sets in other contexts, you may have to be more careful\n",
    "that you do not inadvertently force AMPL to generate a large set of tuples. As an example\n",
    ", consider how you could constrain the volume of all products shipped out of each origin\n",
    "to be less than some amount. You might write either\n",
    "```\n",
    "subject to Supply_All {i in ORIG}:\n",
    "  sum {j in DEST, p in PROD: (i,j,p) in ROUTES}\n",
    "     Trans[i,j,p] <= supply_all[i];\n",
    "```\n",
    "or, using the more compact slice notation,\n",
    "```\n",
    "subject to Supply_All {i in ORIG}:\n",
    "  sum {(i,j,p) in ROUTES} Trans[i,j,p] <= supply_all[i];\n",
    "```\n",
    "In the first case, AMPL explicitly generates the set `{j in DEST, p in PROD}` and\n",
    "checks for membership of `(i,j,p)` in ROUTES`, while in the second case it is able to\n",
    "use a more efficient approach to finding all `(i,j,p)` from `ROUTES` that have a given `i`.\n",
    "In our small examples this may not seem critical, but for problems of realistic size the\n",
    "slice version may be the only one that can be processed in a reasonable amount of time\n",
    "and space."
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Python 3 (ipykernel)",
   "language": "python",
   "name": "python3"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.10.12"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
