{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "c5507de6",
   "metadata": {},
   "source": [
    "# 6.4 Operations on sets of tuples"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "e9568869-9464-4695-a972-4ed81a7e6c17",
   "metadata": {},
   "source": [
    "Operations on compound sets are, as much as possible, the same as the operations\n",
    "introduced for simple sets in [Chapter 5](../05/05.md). Sets of pairs, triples, or longer tuples can be\n",
    "combined with `union`, `inter`, `diff`, and `symdiff`; can be tested by `in` and\n",
    "`within`; and can be counted with `card`. Dimensions of operands must match appropriately,\n",
    "so for example you may not form the union of a set of pairs with a set of triples.\n",
    "Also, compound sets in AMPL cannot be declared as `ordered` or `circular`, and\n",
    "hence also cannot be arguments to functions like first and next that take only\n",
    "ordered sets.\n",
    "\n",
    "Another set operator, `cross`, gives the set of all pairs of its arguments â€” the cross or\n",
    "Cartesian product. Thus the set expression\n",
    "```\n",
    "ORIG cross DEST\n",
    "```\n",
    "represents the same set as the indexing expression `{ORIG,DEST}`, and\n",
    "```\n",
    "ORIG cross DEST cross PROD\n",
    "```\n",
    "is the same as `{ORIG,DEST,PROD}`.\n",
    "\n",
    "Our examples so far have been constructed so that every compound set has a domain\n",
    "within a cross product of previously specified simple sets; `LINKS` lies within `ORIG cross DEST`,\n",
    "for example, and `ROUTES` within `ORIG cross DEST cross PROD`.\n",
    "This practice helps to produce clear and correct models. Nevertheless, if you find it\n",
    "inconvenient to specify the domains as part of the data, you may define them instead\n",
    "within the model. AMPL provides an iterated `setof` operator for this purpose, as in the\n",
    "following example:\n",
    "```\n",
    "set ROUTES dimen 3;\n",
    "set PROD = setof {(i,j,p) in ROUTES} p;\n",
    "set LINKS = setof {(i,j,p) in ROUTES} (i,j);\n",
    "```\n",
    "Like an iterated sum operator, `setof` is followed by an indexing expression and an\n",
    "argument, which can be any expression that evaluates to a legal set member. The argument\n",
    "is evaluated for each member of the indexing set, and the results are combined into\n",
    "a new set that is returned by the operator. Duplicate members are ignored. Thus these\n",
    "expressions for `PROD` and `LINKS` give the sets of all objects `p` and pairs `(i,j)` such\n",
    "that there is some member `(i,j,p)` in `ROUTES`.\n",
    "\n",
    "As with simple sets, membership in a compound set may be restricted by a logical\n",
    "condition at the end of an indexing expression. For example, the multicommodity transportation\n",
    "model could define\n",
    "```\n",
    "set DEMAND = {j in DEST, p in PROD: demand[j,p] > 0};\n",
    "```\n",
    "so that `DEMAND` contains only those pairs `(j,p)` with positive demand for product `p` at\n",
    "destination `j`. As another example, suppose that we also wanted to model transfers of the\n",
    "products from one origin to another. We could simply define\n",
    "```\n",
    "set TRANSF = {ORIG,ORIG};\n",
    "```\n",
    "to specify the set of all pairs of members from `ORIG`. But this set would include pairs\n",
    "like `(\"PITT\",\"PITT\")`; to specify the set of all pairs of different members from\n",
    "`ORIG`, a condition must be added:\n",
    "```\n",
    "set TRANSF = {i1 in ORIG, i2 in ORIG: i1 != i2};\n",
    "```\n",
    "This is another case where two different dummy indices, `i1` and `i2`, need to be defined\n",
    "to run over the same set; the condition selects those pairs where `i1` is not equal to `i2`.\n",
    "\n",
    "If a set is ordered, the condition within an indexing expression can also refer to the\n",
    "ordering. We could declare\n",
    "```\n",
    "set ORIG ordered;\n",
    "set TRANSF = {i1 in ORIG, i2 in ORIG: ord(i1) < ord(i2)};\n",
    "```\n",
    "to define a \"triangular\" set of pairs from `ORIG` that does not contain any pair and its\n",
    "reverse. For example, `TRANSF` would contain either of the pairs `(\"PITT\",\"CLEV\")`\n",
    "or `(\"CLEV\",\"PITT\")`, depending on which came first in `ORIG`, but it would not contain\n",
    "both.\n",
    "\n",
    "Sets of numbers can be treated in a similar way, since they are naturally ordered.\n",
    "Suppose that we want to accommodate inventories of different ages in the multiperiod\n",
    "production model of [Figure 4-4](../04/4_3_a_model_of_production_and_transportation.ipynb#fig-4-4), by declaring:\n",
    "```\n",
    "set PROD;          # products\n",
    "param T > 0;       # number of weeks\n",
    "param A > 0;       # maximum age of inventory\n",
    "var Inv {PROD,0..T,0..A} >= 0; # tons inventoried\n",
    "```\n",
    "Depending on how initial inventories are handled, we might have to include a constraint\n",
    "that no inventory in period `t` can be more than `t` weeks old:\n",
    "```\n",
    "subject to Too_Old\n",
    "  {p in PROD, t in 1..T, a in 1..A: a > t}: Inv[p,t,a] = 0;\n",
    "```\n",
    "In this case, there is a simpler way to write the indexing expression:\n",
    "```\n",
    "subject to Too_Old\n",
    "\t{p in PROD, t in 1..T, a in t+1..A}: Inv[p,t,a] = 0;\n",
    "```\n",
    "\n",
    "Here the dummy index defined by `t in 1..T` is immediately used in the phrase `a in t+1..A`.\n",
    "In this and other cases where an indexing expression specifies two or more\n",
    "sets, the comma-separated phrases are evaluated from left to right. Any dummy index\n",
    "defined in one phrase is available for use in all subsequent phrases."
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Python 3 (ipykernel)",
   "language": "python",
   "name": "python3"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.10.12"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
