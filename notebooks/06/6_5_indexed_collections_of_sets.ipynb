{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "c5507de6",
   "metadata": {},
   "source": [
    "# 6.5 Indexed collections of sets"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "07b80233",
   "metadata": {
    "scrolled": true
   },
   "outputs": [],
   "source": [
    "# install dependencies\n",
    "%pip install -q amplpy\n",
    "\n",
    "from amplpy import AMPL, ampl_notebook\n",
    "\n",
    "ampl = ampl_notebook(\n",
    "    modules=['highs'],  # modules to install\n",
    "    license_uuid='default',  # license to use\n",
    ")  # instantiate AMPL object and register magics"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "e9568869-9464-4695-a972-4ed81a7e6c17",
   "metadata": {},
   "source": [
    "Although declarations of individual sets are most common in AMPL models, sets may\n",
    "also be declared in collections indexed over other sets. The principles are much the same\n",
    "as for indexed collections of parameters, variables or constraints.\n",
    "\n",
    "As an example of how indexed collections of sets can be useful, let us extend the multiperiod\n",
    "production model of [Figure 4-4](../04/4_3_a_model_of_production_and_transportation.ipynb#fig-4-4) to recognize different market areas for each product.\n",
    "We begin by declaring:\n",
    "```\n",
    "set PROD;\n",
    "set AREA {PROD};\n",
    "```\n",
    "This says that for each member `p` of `PROD`, there is to be a set `AREA[p]`; its members\n",
    "will denote the market areas in which product `p` is sold.\n",
    "\n",
    "The market demands, expected sales revenues and amounts to be sold should be\n",
    "indexed over areas as well as products and weeks:\n",
    "```\n",
    "param market {p in PROD, AREA[p], 1..T} >= 0;\n",
    "param revenue {p in PROD, AREA[p], 1..T} >= 0;\n",
    "var Sell {p in PROD, a in AREA[p], t in 1..T}\n",
    "\t\t\t >= 0, <= market[p,a,t];\n",
    "```\n",
    "In the declarations for `market` and `revenue`, we define only the dummy index `p` that is\n",
    "needed to specify the set `AREA[p]`, but for the `Sell` variables we need to define all the\n",
    "dummy indices, so that they can be used to specify the upper bound `market[p,a,t]`.\n",
    "This is another example in which an index defined by one phrase of an indexing expression\n",
    "is used by a subsequent phrase; for each `p` from the set `PROD`, a runs over a different\n",
    "set `AREA[p]`.\n",
    "\n",
    "In the objective, the expression `revenue[p,t]*Sell[p,t]` from [Figure 4-4](../04/4_3_a_model_of_production_and_transportation.ipynb#fig-4-4)\n",
    "must be replaced by a sum of revenues over all areas for product `p`:\n",
    "```\n",
    "maximize Total_Profit:\n",
    "  sum {p in PROD, t in 1..T}\n",
    "     (sum {a in AREA[p]} revenue[p,a,t]*Sell[p,a,t] -\n",
    "\tprodcost[p]*Make[p,t] - invcost[p]*Inv[p,t]);\n",
    "```\n",
    "The only other change is in the `Balance` constraints, where `Sell[p,t]` is similarly\n",
    "replaced by a summation:\n",
    "```\n",
    "subject to Balance {p in PROD, t in 1..T}:\n",
    "  Make[p,t] + Inv[p,t-1]\n",
    "     = sum {a in AREA[p]} Sell[p,a,t] + Inv[p,t];\n",
    "```\n",
    "\n",
    "The complete model is shown in [Figure 6-3](../06/6_5_indexed_collections_of_sets.ipynb#fig-6-3).\n",
    "\n",
    "<a id='fig-6-3'><center><b>Figure 6-3:</b> Multiperiod production with indexed sets (steelT3.mod).</center></a>"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "id": "290b51b2-9c58-43fd-8947-0cb4d800f4b7",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "Writing steelT3.mod\n"
     ]
    }
   ],
   "source": [
    "%%writefile steelT3.mod\n",
    "    \n",
    "set PROD;                                 # products\n",
    "set AREA {PROD};                          # market areas for each product\n",
    "param T > 0;                              # number of weeks\n",
    "param rate {PROD} > 0;          # tons per hour produced\n",
    "param inv0 {PROD} >= 0;         # initial inventory\n",
    "param avail {1..T} >= 0;        # hours available in week\n",
    "param market {p in PROD, AREA[p], 1..T} >= 0;\n",
    "\t\t\t\t\t  # limit on tons sold in week\n",
    "param prodcost {PROD} >= 0;     # cost per ton produced\n",
    "param invcost {PROD} >= 0;      # carrying cost/ton of inventory\n",
    "param revenue {p in PROD, AREA[p], 1..T} >= 0;\n",
    "\t\t\t\t# revenue per ton sold\n",
    "var Make {PROD,1..T} >= 0;      # tons produced\n",
    "var Inv {PROD,0..T} >= 0;       # tons inventoried\n",
    "var Sell {p in PROD, a in AREA[p], t in 1..T}   # tons sold\n",
    "\t\t    >= 0, <= market[p,a,t];\n",
    "maximize Total_Profit:\n",
    "\tsum {p in PROD, t in 1..T}\n",
    "      (sum {a in AREA[p]} revenue[p,a,t]*Sell[p,a,t] -\n",
    "\t prodcost[p]*Make[p,t] - invcost[p]*Inv[p,t]);\n",
    "\t\t\t     # Total revenue less costs for all products in all weeks\n",
    "subject to Time {t in 1..T}:\n",
    "\tsum {p in PROD} (1/rate[p]) * Make[p,t] <= avail[t];\n",
    "\t\t\t     # Total of hours used by all products\n",
    "\t\t\t     # may not exceed hours available, in each week\n",
    "subject to Init_Inv {p in PROD}: Inv[p,0] = inv0[p];\n",
    "\t\t\t     # Initial inventory must equal given value\n",
    "subject to Balance {p in PROD, t in 1..T}:\n",
    "\tMake[p,t] + Inv[p,t-1]\n",
    "      = sum {a in AREA[p]} Sell[p,a,t] + Inv[p,t];\n",
    "\t\t\t     # Tons produced and taken from inventory\n",
    "\t\t\t     # must equal tons sold and put into inventory"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "80651f24-edde-41a1-8121-c85e31cee8d4",
   "metadata": {},
   "source": [
    "In the data for this model, each set within the indexed collection `AREA` is specified\n",
    "like an ordinary set:\n",
    "\n",
    "<a id='fig-6-4'><center><b>Figure 6-4:</b> Data for multiperiod production with indexed sets.</center></a>"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 12,
   "id": "320e3838-1c6d-4320-a9cd-c93022a78f5c",
   "metadata": {},
   "outputs": [],
   "source": [
    "import pandas as pd\n",
    "\n",
    "# Define time periods\n",
    "T = 4\n",
    "weeks = list(range(1, T + 1))\n",
    "\n",
    "# Sets\n",
    "PROD = [\"bands\", \"coils\"]\n",
    "AREA = {\n",
    "    \"bands\": [\"east\", \"north\"],\n",
    "    \"coils\": [\"east\", \"west\", \"export\"]\n",
    "}\n",
    "\n",
    "avail = {1: 40, 2: 40, 3: 32, 4: 40}\n",
    "rate = {\"bands\": 200, \"coils\": 140}\n",
    "inv0 = {\"bands\": 10, \"coils\": 0}\n",
    "prodcost = {\"bands\": 10, \"coils\": 11}\n",
    "invcost = {\"bands\": 2.5, \"coils\": 3}\n",
    "\n",
    "# Revenue data\n",
    "revenue_values = [\n",
    "    [25.0, 26.0, 27.0, 27.0],  # bands-east\n",
    "    [26.5, 27.5, 28.0, 28.5],  # bands-north\n",
    "    [30, 35, 37, 39],          # coils-east\n",
    "    [29, 32, 33, 35],          # coils-west\n",
    "    [25, 25, 25, 28]           # coils-export\n",
    "]\n",
    "# revenue_index is basically: (bands, east), (bands, north),\n",
    "#(coils, east), (coils, west), and (coils, export)\n",
    "revenue_index = [(p, a) for p in PROD for a in AREA[p]]\n",
    "revenue_df = pd.DataFrame(revenue_values, index=pd.MultiIndex.from_tuples(revenue_index, names=[\"p\", \"a\"]), columns=weeks)\n",
    "\n",
    "# Market data, shares same index as revenue\n",
    "market_values = [\n",
    "    [2000, 2000, 1500, 2000],  # bands-east\n",
    "    [4000, 4000, 2500, 4500],  # bands-north\n",
    "    [1000, 800, 1000, 1100],   # coils-east\n",
    "    [2000, 1200, 2000, 2300],  # coils-west\n",
    "    [1000, 500, 500, 800]      # coils-export\n",
    "]\n",
    "market_df = pd.DataFrame(market_values, index=pd.MultiIndex.from_tuples(revenue_index, names=[\"p\", \"a\"]), columns=weeks)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 13,
   "id": "f898d475-c994-4ee5-bbfc-fa3b1975b7f6",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "HiGHS 1.11.0: optimal solution; objective 514521.7143\n",
      "18 simplex iterations\n",
      "0 barrier iterations\n",
      "Objective: 514521.7142857143\n"
     ]
    }
   ],
   "source": [
    "# Instantiate AMPL object\n",
    "ampl = AMPL()\n",
    "\n",
    "# Read model file\n",
    "ampl.read(\"steelT3.mod\")\n",
    "\n",
    "# Load data\n",
    "ampl.set[\"PROD\"] = PROD\n",
    "ampl.set[\"AREA\"] = AREA\n",
    "ampl.param[\"T\"] = T\n",
    "ampl.param[\"avail\"] = avail\n",
    "ampl.param[\"rate\"] = rate\n",
    "ampl.param[\"inv0\"] = inv0\n",
    "ampl.param[\"prodcost\"] = prodcost\n",
    "ampl.param[\"invcost\"] = invcost\n",
    "ampl.param[\"revenue\"].set_values(revenue_df)\n",
    "ampl.param[\"market\"].set_values(market_df)\n",
    "\n",
    "# Solve\n",
    "ampl.solve(solver='highs')\n",
    "print(\"Objective:\", ampl.get_objective(\"Total_Profit\").value())\n"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "72232d9e-c733-4bda-ba2e-bedff095b3b8",
   "metadata": {},
   "source": [
    "The parameters revenue and market are now indexed over three sets, so their data\n",
    "values are specified in a series of tables. Since the indexing is over a different\n",
    "set `AREA[p]` for each product `p`, the values are most conveniently arranged as one table for\n",
    "each product, as shown in [Figure 6-4](../06/6_5_indexed_collections_of_sets.ipynb#fig-6-4). (Chapter 9 xTODO explains the general rules behind this\n",
    "arrangement.)\n",
    "We could instead have written this model with a set `PRODAREA` of pairs, such that\n",
    "product `p` will be sold in area `a` if and only if `(p,a)` is a member of `PRODAREA`. Our\n",
    "formulation in terms of `PROD` and `AREA[p]` seems preferable, however, because it\n",
    "emphasizes the hierarchical relationship between products and areas. Although the model\n",
    "must refer in many places to the set of all areas selling one product, it never refers to the\n",
    "set of all products sold in one area.\n",
    "\n",
    "<a id='fig-6-5'><center><b>Figure 6-5:</b> Multicommodity transportation with indexed sets (multic.mod).</center></a>"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "a337ee8a-6302-4547-9eaf-ee25715173b9",
   "metadata": {},
   "outputs": [],
   "source": [
    "%%writefile multic.mod\n",
    "\n",
    "set ORIG;                       # origins\n",
    "set DEST;                       # destinations\n",
    "set PROD;                       # products\n",
    "set orig {PROD} within ORIG;\n",
    "set dest {PROD} within DEST;\n",
    "set links {p in PROD} = orig[p] cross dest[p];\n",
    "param supply {p in PROD, orig[p]} >= 0; # available at origins\n",
    "param demand {p in PROD, dest[p]} >= 0; # required at destinations\n",
    "check {p in PROD}: sum {i in orig[p]} supply[p,i]\n",
    "\t\t\t = sum {j in dest[p]} demand[p,j];\n",
    "param limit {ORIG,DEST} >= 0;\n",
    "param cost {p in PROD, links[p]} >= 0;  # shipment costs per unit\n",
    "var Trans {p in PROD, links[p]} >= 0;   # units to be shipped\n",
    "minimize Total_Cost:\n",
    "    sum {p in PROD, (i,j) in links[p]} cost[p,i,j] * Trans[p,i,j];\n",
    "subject to Supply {p in PROD, i in orig[p]}:\n",
    "    sum {j in dest[p]} Trans[p,i,j] = supply[p,i];\n",
    "subject to Demand {p in PROD, j in dest[p]}:\n",
    "    sum {i in orig[p]} Trans[p,i,j] = demand[p,j];\n",
    "subject to Multi {i in ORIG, j in DEST}:\n",
    "    sum {p in PROD: (i,j) in links[p]} Trans[p,i,j] <= limit[i,j];\n"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "160bbe19-6246-40f3-aaaf-c1ac910059ee",
   "metadata": {},
   "source": [
    "As a contrasting example, we can consider how the multicommodity transportation\n",
    "model might use indexed collections of sets. As shown in [Figure 6-5](../06/6_5_indexed_collections_of_sets.ipynb#fig-6-5), for each product\n",
    "we define a set of origins where that product is supplied, a set of destinations where the\n",
    "product is demanded, and a set of links that represent possible shipments of the product:\n",
    "```\n",
    "set orig {PROD} within ORIG;\n",
    "set dest {PROD} within DEST;\n",
    "set links {p in PROD} = orig[p] cross dest[p];\n",
    "```\n",
    "The declaration of links demonstrates that it is possible to have an indexed collection\n",
    "of compound sets, and that an indexed collection may be defined through set operations\n",
    "from other indexed collections. In addition to the operations previously mentioned, there\n",
    "are iterated union and intersection operators that apply to sets in the same way that an\n",
    "iterated sum applies to numbers. For example, the expressions\n",
    "```\n",
    "union {p in PROD} orig[p]\n",
    "inter {p in PROD} orig[p]\n",
    "```\n",
    "represent the subset of origins that supply at least one product, and the subset of origins\n",
    "that supply all products.\n",
    "\n",
    "The hierarchical relationship based on products that was observed in [Figure 6-3](../06/6_5_indexed_collections_of_sets.ipynb#fig-6-3) is\n",
    "seen in most of [Figure 6-5](../06/6_5_indexed_collections_of_sets.ipynb#fig-6-5) as well. The model repeatedly deals with the sets of all origins,\n",
    "destinations, and links associated with a particular product. The only exception\n",
    "comes in the last constraint, where the summation must be over all products shipped via a\n",
    "particular link:\n",
    "```\n",
    "subject to Multi {i in ORIG, j in DEST}:\n",
    "      sum {p in PROD: (i,j) in links[p]} Trans[p,i,j] <= limit[i,j];\n",
    "```\n",
    "Here it is necessary, following sum, to use a somewhat awkward indexing expression to\n",
    "describe a set that does not match the hierarchical organization.\n",
    "\n",
    "In general, almost any model that can be written with indexed collections of sets can\n",
    "also be written with sets of tuples. As our examples suggest, indexed collections are most\n",
    "suitable for entities such as products and areas that have a hierarchical relationship. Sets\n",
    "of tuples are preferable, on the other hand, in dealing with entities like origins and destinations\n",
    "that are related symmetrically.\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "d4246227-bf4b-4ec7-afbb-b562763feffa",
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Python 3 (ipykernel)",
   "language": "python",
   "name": "python3"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.10.12"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
