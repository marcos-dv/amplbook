{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "99fedebe",
   "metadata": {},
   "source": [
    "# 2.2 An AMPL model for the diet problem"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "6d6777df",
   "metadata": {},
   "source": [
    "Clearly we will have to consider more extensive modifications to our\n",
    "linear program in order to produce a diet that is even remotely acceptable.\n",
    "We will probably want to change the sets of food and nutrients, as well as\n",
    "the nature of the constraints and bounds.  As in the\n",
    "production example of the previous chapter, this will be much easier to do\n",
    "if we rely on a general model that can be coupled with\n",
    "a variety of specific data files.\n",
    "\n",
    "This model deals with two things: nutrients\n",
    "and foods.  Thus we begin an `AMPL` model by declaring sets of\n",
    "each:\n",
    "```\n",
    "set NUTR;\n",
    "set FOOD;\n",
    "```\n",
    "Next we need to specify the numbers required by the model.  Certainly a\n",
    "positive cost should be given for each food:\n",
    "```\n",
    "param cost {FOOD} > 0;\n",
    "```\n",
    "We also specify that\n",
    "for each food there are lower and upper limits on the number of packages\n",
    "in the diet:\n",
    "```\n",
    "param f_min {FOOD} >= 0;\n",
    "param f_max {j in FOOD} >= f_min[j];\n",
    "```\n",
    "Notice that we need a dummy index\n",
    "`j`\n",
    "to run over\n",
    "`FOOD`\n",
    "in the declaration of\n",
    "`f_max` ,\n",
    "in order to say that the maximum for each food must be greater than\n",
    "or equal to the corresponding minimum.\n",
    "\n",
    "To make this model\n",
    "somewhat more general than our examples so far, we also specify similar lower and upper limits\n",
    "on the amount of each nutrient in the diet:\n",
    "```\n",
    "param n_min {NUTR} >= 0;\n",
    "param n_max {i in NUTR} >= n_min[i];\n",
    "```\n",
    "Finally, for each combination of a nutrient and a food, we need a number\n",
    "that represents the amount of the nutrient in one package of the food.  You\n",
    "may recall from [Chapter 1](../01/01.md) that such a \"product\" of two sets is\n",
    "written by listing them both:\n",
    "```\n",
    "param amt {NUTR,FOOD} >= 0;\n",
    "```\n",
    "References to this parameter require two indices.  For example,\n",
    "`amt[i,j]`\n",
    "is\n",
    "the amount of nutrient\n",
    "`i`\n",
    "in a package of food\n",
    "`j` .\n",
    "\n",
    "The decision variables for this model are the numbers of packages to\n",
    "buy of the different foods:\n",
    "```\n",
    "var Buy {j in FOOD} >= f_min[j], <= f_max[j];\n",
    "```\n",
    "The number of packages of some food\n",
    "`j`\n",
    "to be bought will be called\n",
    "`Buy[j]` ;\n",
    "in any acceptable solution it will have to lie between\n",
    "`f_min[j]`\n",
    "and\n",
    "`f_max[j]` .\n",
    "\n",
    "The total cost of buying a food\n",
    "`j`\n",
    "is the cost per package,\n",
    "`cost[j]` ,\n",
    "times the number of packages,\n",
    "`Buy[j]` .\n",
    "The objective to be minimized is\n",
    "the sum of this product over all foods\n",
    "`j` :\n",
    "```\n",
    "minimize Total_Cost:  sum {j in FOOD} cost[j] * Buy[j];\n",
    "```\n",
    "This\n",
    "`minimize`\n",
    "declaration works the same as\n",
    "`maximize`\n",
    "did in Chapter 1.\n",
    "\n",
    "Similarly, the amount of a nutrient\n",
    "`i`\n",
    "supplied by a food\n",
    "`j`\n",
    "is the nutrient per\n",
    "package,\n",
    "`amt[i,j]` ,\n",
    "times the number of packages\n",
    "`Buy[j]` .\n",
    "The total amount of nutrient\n",
    "`i`\n",
    "supplied is the sum of this product over all foods\n",
    "`j` :\n",
    "```\n",
    "sum {j in FOOD} amt[i,j] * Buy[j]\n",
    "```\n",
    "To complete the model, we need only specify that each\n",
    "such sum must lie between the appropriate bounds.  Our\n",
    "constraint declaration begins\n",
    "```\n",
    "subject to Diet {i in NUTR}:\n",
    "```\n",
    "to say that a constraint named\n",
    "`Diet[i]`\n",
    "must be imposed for each member\n",
    "`i`\n",
    "of\n",
    "`NUTR` .\n",
    "The rest of the declaration gives the algebraic statement of the\n",
    "constraint for nutrient\n",
    "`i` :\n",
    "the variables must satisfy\n",
    "```\n",
    "n_min[i] <= sum {j in FOOD} amt[i,j] * Buy[j] <= n_max[i]\n",
    "```\n",
    "A \"double inequality\" like this is interpreted in the obvious way:  the\n",
    "value of the sum in the middle must lie between\n",
    "`n_min[i]`\n",
    "and\n",
    "`n_max[i]` .\n",
    "The complete model is shown bellow.\n",
    "\n",
    "<a id='fig-2-1'><center><b>Figure 2-1:</b> Diet model in AMPL (diet.mod).</center></a>"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "id": "5c78e876",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "Overwriting diet.mod\n"
     ]
    }
   ],
   "source": [
    "%%writefile diet.mod\n",
    "\n",
    "set NUTR;\n",
    "set FOOD;\n",
    "\n",
    "param cost {FOOD} > 0;\n",
    "param f_min {FOOD} >= 0;\n",
    "param f_max {j in FOOD} >= f_min[j];\n",
    "\n",
    "param n_min {NUTR} >= 0;\n",
    "param n_max {i in NUTR} >= n_min[i];\n",
    "\n",
    "param amt {NUTR,FOOD} >= 0;\n",
    "\n",
    "var Buy {j in FOOD} >= f_min[j], <= f_max[j];\n",
    "\n",
    "minimize Total_Cost:  sum {j in FOOD} cost[j] * Buy[j];\n",
    "\n",
    "subject to Diet {i in NUTR}:\n",
    "   n_min[i] <= sum {j in FOOD} amt[i,j] * Buy[j] <= n_max[i];"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Python 3 (ipykernel)",
   "language": "python",
   "name": "python3"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.10.12"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
