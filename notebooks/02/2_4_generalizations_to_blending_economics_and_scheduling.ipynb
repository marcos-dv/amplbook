{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "6aa8c113",
   "metadata": {},
   "source": [
    "# 2.4 Generalizations to blending, economics and scheduling"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "dffd933f",
   "metadata": {},
   "source": [
    "Your personal experience probably suggests that diet models are not\n",
    "widely used by people to choose their dinners.  These models would be\n",
    "much better suited to situations in which packaging and personal\n",
    "preferences don't play such a prominent role &ndash; for example, the blending of\n",
    "animal feed or perhaps food for college dining halls.\n",
    "\n",
    "The diet model is a convenient, intuitive example\n",
    "of a linear programming formulation that appears in many\n",
    "contexts.  Suppose that we rewrite the model in a more general way, as\n",
    "shown in [Figure 2-4](#fig-2-4).\n",
    "The objects that were called foods and nutrients in the diet model are\n",
    "now referred to more generically as \"inputs\" and \"outputs\".  For each\n",
    "input\n",
    "`j` ,\n",
    "we must decide to use a quantity\n",
    "`X[j]`\n",
    "that lies between\n",
    "`in_min[j]`\n",
    "and\n",
    "`in_max[j]` ;\n",
    "as a result we incur a cost equal to\n",
    "`cost[j]\n",
    "*\n",
    "X[j]` ,\n",
    "and we create\n",
    "`io[i,j]\n",
    "*\n",
    "X[j]`\n",
    "units of each output\n",
    "`i` .\n",
    "Our goal is\n",
    "to find the least-cost combination of inputs that yields, for each\n",
    "output\n",
    "`i` ,\n",
    "an amount between\n",
    "`out_min[i]`\n",
    "and\n",
    "`out_max[i]` .\n",
    "\n",
    "In one common class of applications for this model, the\n",
    "inputs are raw materials to be mixed together.  The outputs are\n",
    "qualities of the resulting blend.  The raw materials could be the\n",
    "components of an animal feed, but they could equally well be the crude\n",
    "oil derivatives that are blended to make gasoline, or the different\n",
    "kinds of coal that are mixed as input to a coke oven.  The qualities\n",
    "can be amounts of something (sodium or calories for animal feed), or\n",
    "more complex measures (vapor pressure or octane rating for gasoline),\n",
    "or even physical properties such as weight and volume.\n",
    "\n",
    "<a id='fig-2-4'><center><b>Figure 2-4:</b>  Least-cost input model (`blend.mod`).</center></a>"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "id": "4875466d",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "Writing blend.mod\n"
     ]
    }
   ],
   "source": [
    "%%writefile blend.mod\n",
    "\n",
    "set INPUT; # inputs\n",
    "set OUTPUT; # outputs\n",
    "\n",
    "param cost {INPUT} > 0;\n",
    "param in_min {INPUT} >= 0;\n",
    "param in_max {j in INPUT} >= in_min[j];\n",
    "\n",
    "param out_min {OUTPUT} >= 0;\n",
    "param out_max {i in OUTPUT} >= out_min[i];\n",
    "\n",
    "param io {OUTPUT,INPUT} >= 0;\n",
    "\n",
    "var X {j in INPUT} >= in_min[j], <= in_max[j];\n",
    "\n",
    "minimize Total_Cost:\n",
    "    sum {j in INPUT} cost[j] * X[j];\n",
    "\n",
    "subject to Outputs {i in OUTPUT}:\n",
    "    out_min[i] <= sum {j in INPUT} io[i,j] * X[j] <= out_max[i];\n"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "f158e2d1-1f50-4f9e-8d29-a008c1e4e4aa",
   "metadata": {},
   "source": [
    "In another well-known application, the inputs are production activities\n",
    "of some sector of an economy, and the outputs are various products.  The\n",
    "`in_min`\n",
    "and\n",
    "`in_max`\n",
    "parameters are limits on the levels of the activities, while \n",
    "`out_min`\n",
    "and\n",
    "`out_max`\n",
    "are regulated by demands.  Thus the goal is to find levels of the activities\n",
    "that meet demand at the lowest cost.  This interpretation is related to the\n",
    "concept of an economic equilibrium, as we will explain in Chapter @Complement@.\n",
    "\n",
    "In still another, quite different application, the inputs are work\n",
    "schedules, and the outputs correspond to hours worked on certain days\n",
    "of a month.  For a particular work schedule\n",
    "`j` ,\n",
    "`io[i,j]`\n",
    "is the number of\n",
    "hours that a person following schedule\n",
    "`j`\n",
    "will work on day\n",
    "`i`\n",
    "(zero if\n",
    "none),\n",
    "`cost[j]`\n",
    "is the monthly salary for a person following schedule\n",
    "`j` ,\n",
    "and\n",
    "`X[j]`\n",
    "is the number of workers assigned that schedule.  Under this\n",
    "interpretation, the objective becomes the total cost of\n",
    "the monthly payroll, while the constraints say that for each day\n",
    "`i` ,\n",
    "the\n",
    "total number of workers assigned to work that day must lie between the\n",
    "limits\n",
    "`out_min[i]`\n",
    "and\n",
    "`out_max[i]` .\n",
    "The same approach can be used in a\n",
    "variety of other scheduling contexts, where the hours, days or months\n",
    "are replaced by other periods of time.\n",
    "\n",
    "Although linear programming can be very useful in applications\n",
    "like these, we need to keep in mind the assumptions that underlie\n",
    "the LP model.  We have already mentioned the \"continuity\" assumption\n",
    "whereby\n",
    "`X[j]`\n",
    "is allowed to take on any value between\n",
    "`in_min[j]`\n",
    "and\n",
    "`in_max[j]` .\n",
    "This may be a lot more reasonable for blending than for\n",
    "scheduling.\n",
    "\n",
    "As another example, in writing the objective as\n",
    "```\n",
    "sum {j in INPUT} cost[j] * X[j]\n",
    "```\n",
    "we are assuming \"linearity of costs\", that is,\n",
    "that the cost of an input is proportional to\n",
    "the amount of the input used, and that the total cost is the sum of the\n",
    "inputs' individual costs.\n",
    "\n",
    "In writing the constraints as\n",
    "```\n",
    "out_min[i] <= sum {j in INPUT} io[i,j] * X[j] <= out_max[i]\n",
    "```\n",
    "we are also assuming that the yield of an\n",
    "output\n",
    "`i`\n",
    "from a particular input is proportional to the amount of the\n",
    "input used, and that the total yield of an output\n",
    "`i`\n",
    "is the sum of the\n",
    "yields from the individual inputs.  This \"linearity of yield\"\n",
    "assumption poses no problem when the inputs are schedules,\n",
    "and the outputs are hours worked.  But in the blending\n",
    "example, linearity is a physical assumption about the nature of the\n",
    "raw materials and the qualities,\n",
    "which may or may not hold.  In early applications to refineries, for\n",
    "example, it was recognized that the addition of lead as an input had a\n",
    "nonlinear effect on the quality known as octane rating in the resulting\n",
    "blend.\n",
    "\n",
    "`AMPL` makes it easy to express discrete\n",
    "or nonlinear models, but any departure from continuity or linearity\n",
    "is likely to make an optimal\n",
    "solution much harder to obtain.  At the least, it takes a more powerful solver\n",
    "to optimize\n",
    "the resulting mathematical programs.  Chapters @Piecewise@ through @Integer@ \n",
    "discuss these issues in more detail. xTODO\n",
    "\n",
    "# Bibliography\n",
    "\n",
    "**George B. Dantzig,** \"The Diet Problem.\"  \n",
    "*Interfaces* **20**, 4 (1990), pp. 43–47.  \n",
    "An entertaining account of the origins of the diet problem.  \n",
    "\n",
    "---\n",
    "\n",
    "**Susan Garner Garille and Saul I. Gass,** \"Stigler's Diet Problem Revisited.\"  \n",
    "*Operations Research* **49**, 1 (2001), pp. 1–13.  \n",
    "A review of the diet problem's origins and its influence over the years on linear programming and on nutritionists.  \n",
    "\n",
    "---\n",
    "\n",
    "**Said S. Hilal and Warren Erikson,** \"Matching Supplies to Save Lives: Linear Programming the Production of Heart Valves.\"  \n",
    "*Interfaces* **11**, 6 (1981), pp. 48–56.  \n",
    "A less appetizing equivalent of the diet problem, involving the choice of pig heart suppliers.  \n"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Python 3 (ipykernel)",
   "language": "python",
   "name": "python3"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.10.12"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
